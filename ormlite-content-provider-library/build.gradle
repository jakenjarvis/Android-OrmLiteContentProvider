apply plugin: 'android-library'
apply plugin: 'maven-publish'

repositories {
    mavenCentral()
}

dependencies {
    androidTestCompile "com.squareup:fest-android:1.0.+"
    compile "com.j256.ormlite:ormlite-core:${project.'ormlite.version'}"
    compile "com.j256.ormlite:ormlite-android:${project.'ormlite.version'}"
}

android {
    compileSdkVersion project.'android-sdk.version'.toInteger()
    buildToolsVersion "${project.'android-buildtools.version'}"

    defaultConfig {
        minSdkVersion project.'android-min-sdk.version'.toInteger()
        targetSdkVersion project.'android-target-sdk.version'.toInteger()
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        // Move the tests to tests/java, tests/res, etc...
        //instrumentTest.setRoot('tests')
        androidTest.setRoot('tests')

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }

    // Note that most of the configuration of the Build Type do not apply to library projects.
    // http://tools.android.com/tech-docs/new-build-system/user-guide#TOC-Differences-between-a-Project-and-a-Library-Project
    buildTypes {
        debug {
            debuggable true
        }
        release {
            debuggable false
        }
    }

    // http://stackoverflow.com/questions/20699147/gradle-build-fails-on-lint-task
    lintOptions {
        abortOnError false
    }
}

ext.javadocLinks = [
    "http://ormlite.com/javadoc/ormlite-core/",
    "http://ormlite.com/javadoc/ormlite-android/"
] as String[]


// http://www.gradle.org/docs/current/dsl/org.gradle.api.tasks.javadoc.Javadoc.html
// http://www.gradle.org/docs/current/userguide/java_plugin.html#sec:javadoc
// http://www.gradle.org/docs/current/javadoc/org/gradle/external/javadoc/StandardJavadocDocletOptions.html
// http://snowdream.github.io/blog/android/2013/11/01/how-to-generate-javadocs-with-android-gradle-plugin/
// https://github.com/chrisbanes/gradle-mvn-push/issues/9
// https://github.com/snowdream/android-autoupdater/blob/master/config/android-library.gradle
task generateJavadoc(type: Javadoc) {
    source = android.sourceSets.main.allJava
    classpath += project.files(android.plugin.getRuntimeJarList().join(File.pathSeparator))
    exclude '**/BuildConfig.java'
    exclude '**/R.java'

    options.docEncoding = rootProject.defaultEncoding
    options.charSet = rootProject.defaultEncoding
    options.links("http://docs.oracle.com/javase/7/docs/api/")
    options.linksOffline("http://d.android.com/reference", "${android.plugin.sdkDirectory}/docs/reference")
    options.links(project.ext.javadocLinks)
}


// http://www.gradle.org/docs/current/dsl/org.gradle.api.tasks.bundling.Zip.html
task artifactAar(type: Zip) {
    extension = 'aar'
    destinationDir = file('build/libs')
}

// http://www.gradle.org/docs/current/dsl/org.gradle.api.tasks.bundling.Jar.html
task artifactJar(type: Jar) {
    from android.sourceSets.main.java
    extension = 'jar'
    destinationDir = file('build/libs/')
}

// http://www.gradle.org/docs/current/dsl/org.gradle.api.tasks.bundling.Zip.html
// https://github.com/googlemaps/android-maps-utils/blob/master/library/build.gradle
task artifactApklib(type: Zip) {
    extension = 'apklib'
    from 'AndroidManifest.xml'
    into('res') {
        from 'res'
    }
    into('src') {
        from 'src'
    }
    destinationDir = file('build/libs/')
}

task artifactSourceJar(type : Jar) {
    from android.sourceSets.main.allSource
    classifier = 'sources'
    extension  = 'jar'
    destinationDir = file('build/libs/')
}

task artifactJavadocJar(type: Jar) {
    dependsOn generateJavadoc
    from generateJavadoc.destinationDir
    classifier = 'javadoc'
    extension  = 'jar'
    destinationDir = file('build/libs/')
}

artifacts {
    archives artifactAar
    archives artifactApklib
    archives artifactJar
    archives artifactSourceJar
    archives artifactJavadocJar
}

// http://www.gradle.org/docs/current/userguide/publishing_maven.html
// http://www.flexlabs.org/2013/06/using-local-aar-android-library-packages-in-gradle-builds
// http://www.rickcarragher.com/blog/2014/01/21/gradle-and-android
android.libraryVariants
publishing {
    publications {
        releaseAar(MavenPublication) {
            artifact artifactAar
        }
        releaseApklib(MavenPublication) {
            artifact artifactApklib
        }
        releaseJar(MavenPublication) {
            artifact artifactJar
        }
        releaseSourceJar(MavenPublication) {
            artifact artifactSourceJar
        }
        releaseJavaDocJar(MavenPublication) {
            artifact artifactJavadocJar
        }
    }
    repositories {
        maven {
            url("../repository")
        }
    }
}
